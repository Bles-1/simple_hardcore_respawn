plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly('net.ultrastudios.lorelink:common:1.0.0-dev.6') {
        capabilities {
            requireCapability 'net.ultrastudios.lorelink:lorelink-common-1.21.4'
        }
    }

    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "net.ultrastudios"
            artifactId = "simple-hardcore-respawn-common"
            version = "2.0.0.r1-SNAPSHOT"
        }
    }
    repositories {
        mavenLocal()
    }
}

